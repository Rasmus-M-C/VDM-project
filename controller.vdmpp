class controller
types
public location = real * real * real;
public parkingLot ::
        car : [car]
        pos : location
        typeOfSlot : <electric> | <gas>
        time : nat
instance variables
   
    public dimensions: nat := 2;
    maxCapacity: nat := (dimensions**3);

    fee: real :=1.5; --  50% added to the price of parking
    electricParkingPrice: real := 1.0;
    gasParkingPrice: real := 5.0;

    
    public gasSlots : map location to parkingLot := {|->};
    public electricSlots : map location to parkingLot := {|->};
    public electricSlotsSeq : seq of location := [];
    public gasSlotsSeq : seq of location := [];

    public parkedCarsMap : map car`licenseplate to car := {|->};  -- Map for parked cars


    bank: real := 0.0;

inv bank >= 0.0;
inv dimensions > 0;

operations

-- flow of program
-- 1. initGarage
-- 2. can a car park
-- set fee if garage is too populated
-- 3  park car
-- 4. remove car when leaving
-- 5. get time spent in garage
-- 6. pay when leaving

-- operation to init garage (garage is currently the controller, should make new class)
--public initGarage: () ==> ()
--initGarage() ==
--(
--    -- Initialize empty sequences
--    electricSlotsSeq := [];
--    gasSlotsSeq := [];
--
--    -- Create electric and gas slots
--    for i = 0 to dimensions-1 do
--        for j = 0 to dimensions-1 do
--            for k = 0 to dimensions-1 do
--                if (i + j + k) mod 3 = 0 then
--                (
--                    -- Add to electric slots
--                    let loc = mk_(i, j, k) in
--                    (
--                        electricSlots := electricSlots ++ {loc |-> mk_parkingLot(nil, loc, <electric>, 0)};
--                        electricSlotsSeq := electricSlotsSeq ^ [loc];
--                    )
--                )
--                else
--                (
--                    -- Add to gas slots
--                    let loc = mk_(i, j, k) in
--                    (
--                        gasSlots := gasSlots ++ {loc |-> mk_parkingLot(nil, loc, <gas>, 0)};
--                        gasSlotsSeq := gasSlotsSeq ^ [loc];
--                    )
--                );
--);



public initGarage: () ==> ()
initGarage() ==
(
    electricSlots := {mk_(i,j,k) |-> mk_parkingLot(nil, mk_(i,j,k), <electric>, 0) | 
    i in set {0,...,dimensions-1}, 
    j in set {0,...,dimensions-1}, 
    k in set {0,...,dimensions-1} 
    & (i + j + k) mod 3 = 0};

    gasSlots := {mk_(i,j,k) |-> mk_parkingLot(nil, mk_(i,j,k), <gas>, 0) | i in 
    set {0,...,dimensions-1}, 
    j in set {0,...,dimensions-1}, 
    k in set {0,...,dimensions-1} 
    & not (i + j + k) mod 3 = 0};

    for all loc in set dom electricSlots do
        electricSlotsSeq := electricSlotsSeq ^ [loc];
    for all loc in set dom gasSlots do
        gasSlotsSeq := gasSlotsSeq ^ [loc];
);

-- operation to park a car in a slot
public parkCar: car * location ==> ()
parkCar(car, loc) ==
(   
    
    if loc in set dom gasSlots then
    (

        gasSlots(loc).car := car;
        gasSlots(loc).time := world`timerRef.getTime();

    )
    elseif loc in set dom electricSlots then
    (
        electricSlots(loc).car := car;
        electricSlots(loc).time := world`timerRef.getTime();
    );
)

pre loc.#1 >=0 and loc.#2 >=0 and loc.#3 >=0 and loc.#1 < dimensions+1 and loc.#2 < dimensions+1 and loc.#3 < dimensions+1;

public enterGarage: car ==> ()
enterGarage(car) ==
(      
           
        if (fullnessRatio(card dom parkedCarsMap+1, getMaxCapacity()) > 0.7)  then
                    car.setFeeApplied(true);
        let loc = findSlotSeq(car) in 
        parkCar(car, loc);
        IO`println("Car with license plate " ^ car.getPlate() ^ " has entered the garage");
        parkedCarsMap := parkedCarsMap ++ {car.getPlate() |-> car};  -- Add to map

    
)
pre card dom parkedCarsMap < maxCapacity and (not carAlreadyExists(car.getPlate()) and findSlotSeq(car) <> mk_(-1, -1, -1));



-- find available slot
public pure findSlot: car * seq of location * seq of location ==> location
findSlot(car, electricSlotSeq, gasSlotSeq) ==
(
    if car.getType() = <electric> then
    (
        if electricSlotSeq <> [] then
        (
            let first = hd electricSlotSeq in
            if isSlotFree(first) then
                return first
            else
                return findSlot(car, tl electricSlotSeq, gasSlotSeq)  -- recur
        )
        elseif gasSlotSeq <> [] then
        (
            let first = hd gasSlotSeq in
            if isSlotFree(first) then
                return first
            else
                return findSlot(car, electricSlotSeq, tl gasSlotSeq)  -- recur
        )
        else
            return mk_(-1, -1, -1);  -- No available slots
    )
    elseif car.getType() = <gas> then
    (
        if gasSlotSeq <> [] then
        (
            let first = hd gasSlotSeq in
            if isSlotFree(first) then
                return first
            else
                return findSlot(car, electricSlotSeq, tl gasSlotSeq)  -- recur
        )
        else
            return mk_(-1, -1, -1);  -- No available slots
    )
    else
        return mk_(-1, -1, -1)  -- default case
);


public pure findSlotSeq : car ==> location
findSlotSeq(car) ==
(
    let electricSlotsSeqCopy = electricSlotsSeq, gasSlotsSeqCopy = gasSlotsSeq in
    return findSlot(car, electricSlotsSeqCopy, gasSlotsSeqCopy);
);

-- deprecated is pre condition now
public canPark: car ==> bool
canPark(car) ==
(
    if not carAlreadyExists(car.getPlate()) then
    (
        if findSlotSeq(car) <> mk_(-1, -1, -1) then
            return true
        else
            return false
    )
    else
        return false
);

-- find specifc licenplate 
public lookupCar: car`licenseplate ==> [car]
lookupCar(plate) ==
(
    if plate in set dom parkedCarsMap then
        return parkedCarsMap(plate)
    else
        return nil;
);

public leaveGarage: car`licenseplate ==> ()
leaveGarage(plate) ==
(
       
        let car = parkedCarsMap(plate) in
        
        (
            for all loc in set dom electricSlots do
            (   
                
                if electricSlots(loc).car = car then
                (   
                    dcl timeSpent : nat := world`timerRef.getTime() - electricSlots(loc).time;
         
                    car.addTime(timeSpent);
                    
                    let amount = pay(car) in  -- payment
                    updateBankBalance(amount);
                    electricSlots(loc).car := nil;  -- free slot
                    car.setLocation(-1, -1, -1);  -- move car to outside garage
                    parkedCarsMap := {plate} <-: parkedCarsMap;
                   return;
                )
            )
        );
        
        let car = parkedCarsMap(plate) in

            for all loc in set dom gasSlots do
            (
                if gasSlots(loc).car = car then
                (
                    dcl timeSpent : nat := world`timerRef.getTime() - gasSlots(loc).time;
         
                    car.addTime(timeSpent);
                    
                    let amount = pay(car) in  -- payment
                    updateBankBalance(amount);
                    gasSlots(loc).car := nil;  -- free slot
                    car.setLocation(-1, -1, -1);  -- move car to outside garage
                    parkedCarsMap := {plate} <-: parkedCarsMap;
                    return;
                )
            )    
)
pre plate in set dom parkedCarsMap;


-- operation to get status of slot
public pure isSlotFree: location ==> bool
isSlotFree(loc) == (
    if loc in set dom gasSlots then
        return gasSlots(loc).car = nil  -- Check if gas slot is free
    elseif loc in set dom electricSlots then
        return electricSlots(loc).car = nil  -- Check if electric slot is free
    else return false;  -- Location is not part of the garage slots
);

-- operation to pay when leaving
public pay: car ==> real
pay(car) ==
(   
    dcl amount :real := 0;
    let timeSpent = car.getTime() in
    let feeApplied = car.getFeeApplied() in
    if feeApplied then
        amount:= fee * timeSpent * priceTypeCar(car)
    else
        amount := timeSpent * priceTypeCar(car);
    
    return amount;
);

public priceTypeCar : car ==> real
priceTypeCar(car) ==
    if car.getType() = <electric> then
        return electricParkingPrice
    else
        return gasParkingPrice;

private updateBankBalance: real ==> ()
updateBankBalance(amount) == (
    bank := bank + amount;

);

pure public carAlreadyExists: car`licenseplate ==> bool
carAlreadyExists(plate) == (
    return plate in set dom parkedCarsMap;
);

-- operation to pay when leaving the garage
pure public getBankBalance: () ==> real
getBankBalance() == (
    return bank;
);
public static natToString : nat ==> seq of char 
		natToString(n) ==
		(
			return VDMUtil`val2seq_of_char[nat](n);
		);
public static realToString : real ==> seq of char
realToString(n) ==
(
    return VDMUtil`val2seq_of_char[real](n);
);
public getMaxCapacity: () ==> nat
getMaxCapacity() == (
    return maxCapacity;
);

functions

fullnessRatio: nat * nat -> real
fullnessRatio(n, max) == 
    n / max
    pre n <= max and max > 0 and n >= 0;
end controller